#!/usr/bin/env zsh
. ~/.myzsh/tool_function.zsh
[[ $- != *i* ]] && return
_fzf_compgen_path() {
  fd --type f  --no-ignore-vcs --hidden --follow --exclude .git --base-directory $1
}

_fzf_compgen_dir() {
  fd --type d  --no-ignore-vcs --hidden --follow  --exclude .git --base-directory $1
}

_git_status() {
  local params=$1
  if [[ -z $params ]] {
    git status -s
  } else {
    eval "git status -s | ${params}"
  }
}

_git_log() {
  git lg
}

_select_git_edit() {
  local str_arr=(${=1})
  local mod=$2
  local remainder=$3
  local is_multi_line=$4
  if (( $is_multi_line == 0 )) {
    echo $str_arr[$remainder]
    return
  }
  local ret_arr=()
  for i ({1..$#str_arr}) {
    if (( $i % $mod == $remainder )) {
      ret_arr+=($str_arr[$i])
    }
  }
  echo $ret_arr 
}

#CTRL_F
wfxr::fzf-edit-selected-widget() {
  local tokens lbuf opt selected params cmd fzf_opt preview_tool base_path cmd_params
  params='-'${LBUFFER##*-}
  if {is_special_opt $params} {
    lbuf=${LBUFFER%-*}
    lbuf=${lbuf/% }
  } else {
    params=''
    lbuf=$LBUFFER
  }
  if {is_dir_param $params} {
    cmd=_fzf_compgen_dir
  } else {
    cmd=_fzf_compgen_path
  }
  tokens=(${=lbuf})
  fzf_opt=($FZF_DEFAULT_COMMON_OPTS)
  preview_tool=($FZF_DEFAULT_PREVIEW_TOOL)
  cmd_params='./'
  local index=0
  local is_multi_line=1
  if (($#tokens > 0)) {
    local head_cmd=$tokens[1]
    if { eval "type _fzf_complete_$head_cmd > /dev/null" } {
      local prefix
      prefix=${tokens[-1]} || prefix=${tokens[-1]:0:-${#trigger}}
      [ -n "${tokens[-1]}" ] && lbuf=${lbuf:0:-${#tokens[-1]}}
      prefix="$prefix" eval _fzf_complete_${tokens[1]} ${(q)lbuf}
      zle reset-prompt
      return 0
    } elif [[ $head_cmd == 'git' ]] {
      cmd=_git_status
      if {is_git_param $params} {
        cmd_params=$(git_file_filter $params)
      } else {
        cmd_params=''
      }
      index=2
      if (( $#tokens > 1 )) {
        if [[ $tokens[2] == 'lg' ]] || [[ $tokens[2] == 'resf' ]] || [[ $tokens[2] == 'rehd' ]] {
          cmd=_git_log
          index=1
          is_multi_line=0
        } elif [[ $tokens[2] == 'diff' ]] {
          cmd_params='grep -E "^ M|^MM"'
        }
      }
      preview_tool=(--preview "$HOME/.myzsh/git-edit.zsh $index {}")
    } else {
      if [[ $head_cmd == 'cd' ]] {
        cmd=_fzf_compgen_dir
      }
      local dir=${tokens[-1]/#\~/$HOME}
      if [[ -d $dir ]] { 
        cmd_params=$dir
      }
    }
  }
  fzf_opt+=($fzf_opt $preview_tool)
  selected=$($cmd $cmd_params | fzf $fzf_opt)
  if (( $index == 0 )) || [[ -z $selected ]] {
    LBUFFER=$lbuf$selected
  } else {
    local mod=2
    LBUFFER=$lbuf' '$(_select_git_edit $selected $mod $(($index % $mod)) $is_multi_line)
  }
  zle reset-prompt
}
zle     -N    wfxr::fzf-edit-selected-widget
bindkey '^F' wfxr::fzf-edit-selected-widget

# CTRL-R - Paste the selected command from history into the command line
wfxr::fzf-history-widget() {
    local selected num
    setopt localoptions noglobsubst noposixbuiltins pipefail 2> /dev/null
    selected=$(fc -rl 1 | sort -uk2,1000 | sort -nr | fzf $FZF_DEFAULT_OPTS  --query=$LBUFFER)
    local ret=$?
    if (( $ret == 0 )) {
      local arr=(${=selected})
      LBUFFER=$arr[2,-1]
    }
    zle reset-prompt
    return ret
}
zle     -N   wfxr::fzf-history-widget
bindkey '^R' wfxr::fzf-history-widget

FZF_MOVE_LAST_POS=0
FZF_MOVE_IS_RIGHT=1
wfxr::fzf_move_cursor_right() {
  sor_pos=$(($CURSOR + ${#RBUFFER} / 2))
  if [ $FZF_MOVE_IS_RIGHT -eq 0 ] && [ $CURSOR -lt $FZF_MOVE_LAST_POS ];then
    sor_pos=$(($CURSOR / 2 + $FZF_MOVE_LAST_POS / 2))
  fi
  FZF_MOVE_LAST_POS=$CURSOR
  CURSOR=$sor_pos
  FZF_MOVE_IS_RIGHT=1
}
zle -N wfxr::fzf_move_cursor_right
bindkey '^N' wfxr::fzf_move_cursor_right

wfxr::fzf_move_cursor_left() {
  sor_pos=$(($CURSOR - ${#LBUFFER} / 2))
  if [ $FZF_MOVE_IS_RIGHT -eq 1 ] && [ $CURSOR -gt $FZF_MOVE_LAST_POS ];then
    sor_pos=$(($CURSOR / 2 + $FZF_MOVE_LAST_POS / 2))
  fi
  FZF_MOVE_LAST_POS=$CURSOR
  CURSOR=$sor_pos
  FZF_MOVE_IS_RIGHT=0
}
zle -N wfxr::fzf_move_cursor_left
bindkey '^B' wfxr::fzf_move_cursor_left
