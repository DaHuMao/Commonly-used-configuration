#!/usr/bin/env zsh
[[ $- != *i* ]] && return
_fzf_compgen_path() {
  fd --type f  --no-ignore-vcs --hidden --follow --exclude .git --base-directory $1
}

_fzf_compgen_dir() {
  fd --type d  --no-ignore-vcs --hidden --follow  --exclude .git --base-directory $1
}

# ALT-E - Edit selected file
wfxr::fzf-file-edit-widget() {
  local tokens cmd prefix matches lbuf d_cmds
  setopt localoptions noshwordsplit noksh_arrays noposixbuiltins

  # http://zsh.sourceforge.net/FAQ/zshfaq03.html
  # http://zsh.sourceforge.net/Doc/Release/Expansion.html#Parameter-Expansion-Flags
  tokens=(${(z)LBUFFER})
  if [ ${#tokens} -lt 1 ]; then
    zle ${fzf_default_completion:-expand-or-complete}
    return
  fi

  cmd=$(__fzf_extract_command "$LBUFFER")

  [ ${LBUFFER[-1]} = ' ' ] && tokens+=("")

  # When the trigger starts with ';', it becomes a separate token
  if [[ ${LBUFFER} = *"${tokens[-2]}${tokens[-1]}" ]]; then
    tokens[-2]="${tokens[-2]}${tokens[-1]}"
    tokens=(${tokens[0,-2]})
  fi

  lbuf=$LBUFFER

  # Trigger sequence given
  d_cmds=(${=FZF_COMPLETION_DIR_COMMANDS:-cd pushd rmdir})

  prefix=${tokens[-1]} || prefix=${tokens[-1]:0:-${#trigger}}
  [ -n "${tokens[-1]}" ] && lbuf=${lbuf:0:-${#tokens[-1]}}

  if eval "type _fzf_complete_${cmd} > /dev/null"; then
    prefix="$prefix" eval _fzf_complete_${cmd} ${(q)lbuf}
    zle reset-prompt
  elif [ ${d_cmds[(i)$cmd]} -le ${#d_cmds} ]; then
    _fzf_dir_completion "$prefix" "$lbuf"
  else
    _fzf_path_completion "$prefix" "$lbuf"
  fi
  # Fall back to default completion
}
zle     -N    wfxr::fzf-file-edit-widget
bindkey '^F' wfxr::fzf-file-edit-widget

# CTRL-R - Paste the selected command from history into the command line
wfxr::fzf-history-widget() {
    local selected num
    setopt localoptions noglobsubst noposixbuiltins pipefail 2> /dev/null
    selected=( $(fc -rl 1 | sort -uk2,1000 | sort -nr |
        FZF_DEFAULT_OPTS="--height ${FZF_TMUX_HEIGHT:-40%} $FZF_DEFAULT_OPTS -n2..,.. --tiebreak=index --bind=ctrl-r:toggle-sort $FZF_CTRL_R_OPTS --query=${(qqq)LBUFFER} +m" $(__fzfcmd)) )
    local ret=$?
    if [ -n "$selected" ]; then
        num=$selected[1]
        if [ -n "$num" ]; then
            zle vi-fetch-history -n $num
        fi
    fi
    zle redisplay
    typeset -f zle-line-init >/dev/null && zle zle-line-init
    return $ret
}
zle     -N   wfxr::fzf-history-widget
bindkey '^R' wfxr::fzf-history-widget

FZF_MOVE_LAST_POS=0
FZF_MOVE_IS_RIGHT=1
wfxr::fzf_move_cursor_right() {
  sor_pos=$(($CURSOR + ${#RBUFFER} / 2))
  if [ $FZF_MOVE_IS_RIGHT -eq 0 ] && [ $CURSOR -lt $FZF_MOVE_LAST_POS ];then
    sor_pos=$(($CURSOR / 2 + $FZF_MOVE_LAST_POS / 2))
  fi
  FZF_MOVE_LAST_POS=$CURSOR
  CURSOR=$sor_pos
  FZF_MOVE_IS_RIGHT=1
}
zle -N wfxr::fzf_move_cursor_right
bindkey '^N' wfxr::fzf_move_cursor_right

wfxr::fzf_move_cursor_left() {
  sor_pos=$(($CURSOR - ${#LBUFFER} / 2))
  if [ $FZF_MOVE_IS_RIGHT -eq 1 ] && [ $CURSOR -gt $FZF_MOVE_LAST_POS ];then
    sor_pos=$(($CURSOR / 2 + $FZF_MOVE_LAST_POS / 2))
  fi
  FZF_MOVE_LAST_POS=$CURSOR
  CURSOR=$sor_pos
  FZF_MOVE_IS_RIGHT=0
}
zle -N wfxr::fzf_move_cursor_left
bindkey '^B' wfxr::fzf_move_cursor_left
